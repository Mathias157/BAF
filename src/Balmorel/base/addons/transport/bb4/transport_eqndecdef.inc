* Transport add-on. Added by Juan Gea Bermudez



EQUATION  QTRANSPEAKDEMAND(Y,RRR,S,T)   'Peak demand (MW)';

QTRANSPEAKDEMAND(IY411,IR,IS3,T)$(TRANSDEMAND_Y(IY411,IR)
$ifi %TRANSPORT_DEMAND_AGGREGATED%==yes OR SUM(IRI,TRANSDEMAND_Y(IY411,IRI))
)..
VTRANSDEMAND_T_MAX(IY411,IR)
=G=
VTRANSDEMAND_T(IY411,IR,IS3,T)

$include "../../base/addons/_hooks/qtranspeakdemand.inc"
;


$ifi %import_results%==yes $ifi %ADDSEASONALTRANSPORTDEMAND%==yes     $goto $SEASONALTRANSPORTDEMAND
EQUATION  QTRANSDEMAND_YNA(Y)    'Annual transport aggregated demand has to be met (MWh)';

QTRANSDEMAND_YNA(IY411)$SUM(IR,TRANSDEMAND_Y(IY411,IR))..
SUM(IR,
   SUM((IS3,T),
      VTRANSDEMAND_T(IY411,IR,IS3,T)*IHOURSINST(IS3,T))
)
=E=
SUM(IR,TRANSDEMAND_Y(IY411,IR))
;

EQUATION  QTRANSDEMAND_YR(Y,RRR)    'Annual transport demand has to be met in each region (MWh)';

QTRANSDEMAND_YR(IY411,IR)$TRANSDEMAND_Y(IY411,IR)..
   SUM((IS3,T),
      VTRANSDEMAND_T(IY411,IR,IS3,T)*IHOURSINST(IS3,T))
=E=
TRANSDEMAND_Y(IY411,IR)
;

EQUATION  QTRANSPEAKDEMANDLIMIT(Y,RRR)    'Peak demand limited(MW)';

QTRANSPEAKDEMANDLIMIT(IY411,IR)$((TRANS_PEAKAVG_RATIO GT 0) AND (TRANSDEMAND_Y(IY411,IR)
$ifi %TRANSPORT_DEMAND_AGGREGATED%==yes OR SUM(IRI,TRANSDEMAND_Y(IY411,IRI))
))..
SUM((IS3,T),VTRANSDEMAND_T(IY411,IR,IS3,T))/SUM((IS3,T),1)*TRANS_PEAKAVG_RATIO
 =G=
VTRANSDEMAND_T_MAX(IY411,IR)
;
$label $SEASONALTRANSPORTDEMAND

$ifi not %import_results%==yes $goto $NO_SEASONALTRANSPORTDEMAND
$ifi     %import_results%==yes $ifi not %ADDSEASONALTRANSPORTDEMAND%==yes  $goto $NO_SEASONALTRANSPORTDEMAND

EQUATION  QTRANSDEMAND_SNA(Y,S)    'Seasonal transport aggregated demand has to be met (MWh)';

QTRANSDEMAND_SNA(IY411,IS3)$SUM(IR,TRANSDEMAND_Y(IY411,IR))..
SUM(IR,
   SUM(T,
      WEIGHT_S(IS3)/IWEIGHSUMT * VTRANSDEMAND_T(IY411,IR,IS3,T))
)
=E=
SUM(IR,TRANSDEMAND_S(IY411,IR,IS3))
;

EQUATION  QTRANSDEMAND_SR(Y,RRR,S)    'Seasonal transport demand has to be met in each region (MWh)';
QTRANSDEMAND_SR(IY411,IR,IS3)$TRANSDEMAND_Y(IY411,IR)..
   SUM(T,
      WEIGHT_S(IS3)/IWEIGHSUMT * VTRANSDEMAND_T(IY411,IR,IS3,T))
=E=
TRANSDEMAND_S(IY411,IR,IS3)
;

$label $NO_SEASONALTRANSPORTDEMAND

$ifi not %import_results%==yes $goto $NO_TERMTRANSPORTDEMAND
$ifi     %import_results%==yes $ifi not %ADDTERMTRANSPORTDEMAND%==yes  $goto $NO_TERMTRANSPORTDEMAND

EQUATION  QTRANSDEMAND_T(Y,RRR,S,T)    'Transport demand per term has to be met (MWh)';

QTRANSDEMAND_T(IY411,IR,IS3,T)..
VTRANSDEMAND_T(IY411,IR,IS3,T)
=E=
TRANSDEMAND_T(IY411,IR,IS3,T)
;

$label $NO_TERMTRANSPORTDEMAND


